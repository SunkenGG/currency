plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta9'
    id("xyz.jpenilla.run-paper") version "2.3.1"
}

group = 'gg.sunken'
version = '1.0.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url 'https://jitpack.io' }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT"
    implementation 'org.mongodb:mongodb-driver-sync:5.3.0'

    annotationProcessor 'org.projectlombok:lombok:1.18.34'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"
}

build {
    dependsOn shadowJar
}

runServer {
    minecraftVersion("1.21.4")
}

shadowJar {
    archiveBaseName.set('Currency')
    archiveClassifier.set('')
    archiveVersion.set(version)
    minimize()

    relocate('org.bson', 'gg.sunken.currency.libs.org.bson')
    relocate('org.mongodb', 'gg.sunken.currency.libs.org.mongodb')

    // remove meta-INF
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/versions/**'
    exclude 'META-INF/INDEX.LIST'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/DEPENDENCIES.txt'
    exclude 'META-INF/versions/9/**'
    exclude 'META-INF/native-image/**'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
